// Top-level build file where you can add configuration options common to all sub-projects/modules.
def majorVersion = 1
def minorVersion = 4
def patchVersion = 3
def buildNumber = 1
def buildVersionCode
def buildVersionName
buildVersionCode = majorVersion * 10000000 + minorVersion * 100000 + patchVersion * 100 + buildNumber
buildVersionName = "${majorVersion}.${minorVersion}.${patchVersion} build ${buildNumber}"

buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
ext {
    applicationId = "org.dvbviewer.controller"
    compileSdkVersion = project.compileSdkVersion.toInteger()
    buildToolsVersion = project.buildToolsVersion
    minSdkVersion = project.minSdkVersion.toInteger()
    targetSdkVersion = project.targetSdkVersion.toInteger()
    versionCode = buildVersionCode
    versionName = buildVersionName
}
allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex') ) {
                details.useVersion "28.0.0"
            }
        }
    }
}