apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.versionName
        versionCode rootProject.versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard.cfg'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    dependencies {
        compile project(':showCaseView')
        compile 'com.android.support:appcompat-v7:' +project.compatVersion
        compile 'com.android.support:design:'+ project.compatVersion
        compile 'com.android.support:preference-v7:' + project.compatVersion
        compile 'com.android.support:cardview-v7:' + project.compatVersion
        compile 'com.android.support:gridlayout-v7:' + project.compatVersion
        compile 'com.google.android.gms:play-services-analytics:' + project.playServiceVersion
        compile 'com.google.firebase:firebase-crash:' + project.playServiceVersion
        compile 'com.google.firebase:firebase-core:' + project.playServiceVersion
        compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.1'
        compile 'com.google.code.gson:gson:2.8.1'
        compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
        compile 'org.apache.commons:commons-lang3:3.5'
        compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
        compile group: 'commons-io', name: 'commons-io', version: '2.5'
        compile 'com.jakewharton:butterknife:8.8.1'
        annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
        testCompile 'junit:junit:4.12'
        androidTestCompile 'commons-io:commons-io:2.5'
        androidTestCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.1'
        androidTestCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.6.0'
        androidTestCompile('com.android.support.test:runner:0.5') {
            // Necessary if your app targets Marshmallow (since the test runner
            // hasn't moved to Marshmallow yet)
            exclude group: 'com.android.support', module: 'support-annotations'
        }

        // Set this dependency to use JUnit 4 rules
        androidTestCompile('com.android.support.test:rules:0.5') {
            // Necessary if your app targets Marshmallow (since the test runner
            // hasn't moved to Marshmallow yet)
            exclude group: 'com.android.support', module: 'support-annotations'
        }
        // Set this dependency to build and run Espresso tests
        androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
            exclude group: 'com.android.support', module: 'support-annotations'
            exclude group: 'com.android.support', module: 'support-v4'
            exclude group: 'com.android.support', module: 'design'
            exclude group: 'com.android.support', module: 'recyclerview-v7'
        }
    }

}

Properties props = new Properties()
def propFile = file('../keystore/signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
}else {
    android.buildTypes.release.signingConfig = null
}

apply plugin: 'com.google.gms.google-services'